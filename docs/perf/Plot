#! /usr/bin/jconsole
PATH =: ({.~ 1+i:&'/')@}:^:(1+'.'&e.) >{:4!:3 ''
require PATH,'timing.ijs'
assert 3 4 e.~ #ARGV

rnd =: 0.5 <.@:+ ]

q =: '"'&(,,[)
nr =: rplc i.@# ('\'&,&.>@[,@,.q&.>@])&(":&.>) ]
tr =: rplc '\t' ; ]

brak =: [:<'<',,&'>'
surr =: brak@[ , (' ',&.>boxxopen@]) , brak@('/',' 'taketo[)
g =: ('g ',[) surr ]

OP =: 2{::ARGV
L =: 5
N =: 10 ^ L

COLORS =: '#7ccf81';'#6ca67d';'#db6239';'#c23f25'
knames =: 'gcc';'gcc (native)';'Dyalog 17.1';'Dyalog 18.0'
ALL_DATA =: ".@,~&(OP,'_')&.> ;:'gcc nat v17 v18'
YRANGE =: 20 2000
YUNIT =: 'ps'

CIND =: 0

draw =: 4 : 0
  S =. x
  'X Y W H' =. y
  DIM =. 220 490+(X+W),(Y+H)
  scalex =. X + W&*
  xfp =. [: rnd@:scalex N&^.
  scaley =. (Y+H) - H&*
  gety =. [: rnd@:scaley YRANGE %~/@:({.@[ -~ {:@[,:])&:^. ]
  DATA =. (2^.S) {&> ALL_DATA

  yv =. ((]#~1=I.) (>:i.9) ,@:(*/~) ([:({.+i.@:(-~/))_1 0+>.)&.:(10&^.)) _1 0+YRANGE
  ylines =. (#~0=2|(%(_1+>.)&.(10&^.))) yv
  yticks =. (< #&.>~ [:(;-.)(=<.)@:(10&^.)) yv

  pfmt =. rplc&((]&.".&.> ,@:,.~ '%',&.>])'S';'OP')

  line =. ('stroke-width="23" stroke="\t" stroke-linejoin="round" stroke-linecap="round" fill="none"'tr[) (g<) '<path d="\t" />'tr]
  series =. (xfp l) <@:,."1 gety DATA
  LINES =. COLORS line&.>(;@:) ((<@,":)"_1~(;@:)'ML'#~1,<:@#)&.> series

  tickpos=. (,N%&.>]) (,&N ([;-.~) [:,*/&(>:i.9)) 10^i.L
  labpos =. 10^i.>:L

  TICKT =. '<path fill="none" stroke-width=\0 stroke="Black" d="\t"/>'
  path =. tr 'ML'<@(,":)"0 1"1 2(;@:)]
  getticks =. (TICKT nr [) path ]
  mmticks =. getticks&.>~ 7 3$~$
  tlen =. 52;34
  TICK =. mmticks (,/ tlen ([:+/\]*1,[)"0 1~&.>/~ <"1(Y+H),._1 1) (,."0 1~ xfp)&.> tickpos
  YTICK=. mmticks (<"1 X-0,.~52 36) (,.~"0 1~ gety)&.> yticks

  fmte =. ":`(((%10&^)([,'e',])&":])10<.@^.])@.(1e3&<:)"0
  label=. (]nr~'<text x=\1 y=\0>\t</text>' tr fmte@[)(<@)"0 1
  xlabpos =. (Y+H-70)&,. , (Y+H+130),.(W+2*X)&-
  XLAB =. (,~ label [: (+(,:10 30){.~#)@:xlabpos (X+W-28)<.xfp) labpos
  YLAB =. (label (X-65),.~26+gety) ylines

  GRID =. <('Black';'Silver') rplc~ 1.8 getticks (X+0,W) ,."1 0 gety ylines
  XGRID=. <('Black';'Silver') rplc~ 1.8 getticks (Y+0,H) ,.~"1 0 xfp 10^i.>:L
  AXES =. <12 getticks (X,.Y+_44,~H+52),:(Y+H),.~X+_44,W+44

  XCAP =. ((X+W*(,-.)0.09),&.>_84 152+Y+H) ([nr~'<text x=\0 y=\1 style="font-size: 100px;" fill="#444">\t</text>'tr])&.> 'Left';'Right'

  YLABS=.'font-size="80" text-anchor="end"' g YLAB
  XLABS=.'font-size="80" text-anchor="middle"' g XLAB,XCAP

  keyy =. 46+100*i.#knames
  KEYT =. 'stroke-width="19" stroke-linecap="round" fill="#12061a" transform="translate(\t)"' tr ":X,Y-16
  KEYRECT=. '<rect fill="White" stroke="#0b0615" stroke-width="4" x="50" y="-6" width=\0 height=\1 />' nr 720,120+(>./-<./)keyy
  KEYSTR =. COLORS ('<path stroke="COLOR" d="PATH"/>' rplc 'COLOR';[;'PATH';])&.> keyy ('\t'path,.~)&.> <100 220
  KEYCAP =. knames (]nr~'<text x=\0 y=\1 style="font-size: 80px;">\t</text>'tr[)&.> 250,&.>keyy+30
  KEY =. KEYT g KEYRECT;KEYSTR,KEYCAP

  cname =. 'cut',":CIND
  CIND =: CIND+1
  CLIP =. 'defs' surr ('clipPath id="',cname,'"') surr '<rect x=\0 y=\1 width=\2 height=\3 />' nr X,Y,W,H
  LINES =. ('clip-path="url(#',cname,')"') g LINES

  'font-family="DejaVu Sans"' g CLIP,GRID,XGRID,TICK,YTICK,LINES,AXES,YLABS,XLABS,KEY
)

getcaps =: 4 : 0
  'X Y W H Cs Ts' =. y
  YCAP =. '_-'rplc~('<text transform="rotate(-90)" x=\0 y=\1 dy="0.9em">Time ÷ output bound (\t)</text>'tr YUNIT) nr (20-Y+-:H),X-440
  XCAP =. '<text x=\0 y=\1>Argument lengths</text>' nr (X+-:W),320+Y+H
  TITLE =. x tr~ ('\F';":Ts) rplc~ '<text x=\0 y=\1 style="font-size:\Fpx; text-anchor:middle;">\t</text>' nr (X+-:W),_160+Y
  ('font-size="',(":Cs),'px" text-anchor="middle"') g TITLE ; YCAP ; XCAP
)

printtype =: ":,'-byte ',('integers';'floats'){::~=&8
OPS =: |: (({.~;(}.~>:)) i.&' ');._2 ]0 : 0
min Min (⌊)
plus Plus (+)
times Times (×)
lt Less-Than (&lt;)
and And (∧)
ne Not-Equals (≠)
)

3 :0 ]3}.ARGV
  OPIND =. ({.OPS) i. <OP
  OPNAME =. (OPIND {:: {:OPS),' outer product'
  FILE =. OP
  if. OPIND>:4 do.
    COLORS =: COLORS , <'#c7126c'
    knames =: knames , <'Dyalog 18.0'
    ALL_DATA =: ,:&.> ALL_DATA,<".OP,'_x18'
    YRANGE =: 4 2000
    printtype =: 'booleans'"_
    y =. ;:'0'
  end.
  if. #y do.
    XYWH =. 520 460 3700 2060
    DIM =. 300 490++/_2]\XYWH
    S =. 2 ^ ". 0{::y
    FILE =. FILE,(OPIND<4)#'_',":S
    TNAME =. printtype S
    GRAPH =. (S draw XYWH) , (OPNAME,' on ',TNAME) getcaps XYWH,110 140
  else.
    XYWH =. 660 680,0.8<.@*3700 2060
    DIM =. 300 _194++/_2]\XYWH
    draw_t =. draw (,<) printtype@[ tr~ '<text x=\0 y=\1 style="font-size:120px; text-anchor:end;" fill="#444">\t</text>' nr 2({.+_60,~{)]
    GRAPH =. ; (2^i.4) <@(draw_t XYWH+4{.DIM&*)"0 1 ,/,~"0/~0 1
    DIM =. 2*DIM
    GRAPH =. GRAPH , OPNAME getcaps ((,(DIM-60 _240)&-)100 200-~2{.XYWH),160 200
    DIM =. DIM + _120 700
  end.
  BACK =. <'<g fill="White"><rect x="0" y="0" width=\0 height=\1 /></g>' nr DIM
  HEAD_T =. 'svg version="1.1" baseProfile="full" \t xmlns="http://www.w3.org/2000/svg" xmlns:xlink="http://www.w3.org/1999/xlink" xmlns:ev="http://www.w3.org/2001/xml-events" '
  HEADER =. HEAD_T tr ((0 0":@:,]) tr~ 'width=\0 height=\1 viewBox="\t"' nr %&4) DIM
  SVG =. ;,&LF&.> HEADER surr BACK,GRAPH
  SVG 1!:2 <PATH,'OuterPerf_',FILE,'.svg'
)
exit ''
